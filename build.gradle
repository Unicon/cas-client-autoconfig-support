apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'propdeps'
apply from: 'http://tellurianring.com/projects/gradle-plugins/gradle-release/apply.groovy'

sourceCompatibility = 1.7
targetCompatibility = 1.7

buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath "org.springframework.build.gradle:propdeps-plugin:${propdepsVersion}"
    }
}

configure(allprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
}

repositories {
    jcenter()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter:${springBootVersion}"
    compile "javax.validation:validation-api:${validationApiVersion}"

    //Deps for CAS client
    compile "org.jasig.cas.client:cas-client-core:${casClientVersion}"
    compile "org.opensaml:opensaml:${openSamlVersion}"
    compile "xml-apis:xml-apis:${xmlApisVersion}"

    provided "javax.servlet:javax.servlet-api:${servletApiVersion}"

    //Configuration metadata generation - for a nice IDE intellisence support
    optional "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"
}

//Configuration metadata generation
compileJava.dependsOn(processResources)

//turning off unacceptable Javadoc lint in JDK 8+ which prevents builds with "non-strict" HTML5 javadoc tags
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
}

javadoc {
    classpath = configurations.compile + configurations.provided
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

task wrapper(type: Wrapper) {
    gradleVersion = 2.3
}


createReleaseTag.dependsOn uploadArchives

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: 'https://api.bintray.com/maven/uniconiam/maven/cas-client-autoconfig-support') {
                authentication(userName: bintrayUsername, password: bintrayAPIKey)
            }
            pom.project {
                name 'CAS client autoconfig support'
                packaging 'jar'
                description 'Annotation-based configuration support for Apereo CAS Java clients'
                url 'https://github.com/Unicon/cas-client-autoconfig-support'

                scm {
                    url 'scm:git@github.com:Unicon/cas-client-autoconfig-support.git'
                    connection 'scm:git@github.com:Unicon/cas-client-autoconfig-support.git'
                    developerConnection 'scm:git@github.com:Unicon/cas-client-autoconfig-support.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'dima767'
                        name 'Dmitriy Kopylenko'
                    }
                }
            }
        }
    }
}
